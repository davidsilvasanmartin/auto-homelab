set dotenv-load := true

uv := "uv"
docker := "docker"

# [ HELP] List available commands. Gets executed when running `just` with no args
default:
    @just --list --unsorted

# [ðŸ”§ APP] Interactive script that creates a `.env.<timestamp>` file
configure:
    {{uv}} run --env-file=.env -m scripts.configure

# [ðŸ”§ APP] Starts a service, or all services if one is not specified. Example: `just start` // `just start calibre`
start service="":
    if [ -n "{{service}}" ]; then {{docker}} compose up -d {{service}}; else {{docker}} compose up -d; fi

# [ðŸ”§ APP] Stops a service, or all services if one is not specified. Example: `just stop` // `just stop calibre`
stop service="":
    if [ -n "{{service}}" ]; then {{docker}} compose stop {{service}}; else {{docker}} compose stop; fi

# [ðŸ”§ APP] Creates a local backup of all services' data
backup-local:
    {{uv}} run --env-file=.env -m scripts.backup

# [ðŸ”§ APP] Syncs the local backup to the cloud. The `backup-local` must be ran first
backup-cloud:
    {{uv}} run --env-file=.env -m scripts.backup

# [ðŸ”§ APP] Restores the paperless-ngx data
restore-paperless:
    ./scripts-shell/restore_paperless.sh

# [ðŸ”§ APP] Restores the immich data
restore-immich:
    ./scripts-shell/restore_immich.sh

# [ðŸ§ª DEV] Runs the tests of Python scripts
dev-test:
    {{uv}} run --env-file=.env -m pytest scripts/tests

# TODO commands for bootstrapping the Python project? As in, installing dependencies for the first time. uv something ??

# [ðŸ§ª DEV] Add development dependencies with uv. Example: `just add-dev "black>=24.8,<25" isort mypy`
dev-add-dev +pkgs:
    {{uv}} add --dev {{pkgs}}

# [ðŸ§ª DEV] Lint the whole project and auto-fix what Ruff safely can
dev-lint:
    {{uv}} run ruff check . --fix

# [ðŸ§ª DEV] Checks that files are formatted correctly
dev-fmt-check:
    {{uv}} run ruff format . --check

# [ðŸ§ª DEV] Format all files
dev-fmt:
    {{uv}} run ruff format .

# [ðŸ§ª DEV] Explains a linting rule. Example: `just dev-explain F401`
dev-explain linting-rule:
    {{uv}} run ruff rule {{linting-rule}}

# [ðŸ“Š STAT] Count lines of code (optional dir argument, default "."). Example: `just stat-loc` or `just stat-loc scripts`
stat-loc dir=".":
    {{uv}} run -m scripts.loc {{dir}}
